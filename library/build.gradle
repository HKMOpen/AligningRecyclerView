/*
 * AligningRecyclerView (c) by Jorge Antonio Diaz-Benito Soriano
 *
 * AligningRecyclerView is licensed under a Creative Commons Attribution 4.0 International License.
 *
 * You should have received a copy of the license along with this work. If not, see http://creativecommons.org/licenses/by/4.0/.
 */

buildscript {
  repositories {
    jcenter()
  }

  //noinspection GroovyAssignabilityCheck
  dependencies {
    //noinspection GroovyAssignabilityCheck
    classpath classpathDependencies.gradleVersions
    //noinspection GroovyAssignabilityCheck
    classpath classpathDependencies.gradleAndroid
    //noinspection GroovyAssignabilityCheck
    classpath classpathDependencies.mavenGradleAndroid
  }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.android.library'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    versionCode 1
    versionName "0"
    minSdkVersion 7
    targetSdkVersion 23
  }

  def ciServer = 'TRAVIS'
  def executingOnCI = "true".equals(System.getenv(ciServer))

  lintOptions {
    if (executingOnCI)
      disable 'NewApi' //For some stupid reason Travis pops NewApi, even though I have no code.

    abortOnError true

    absolutePaths true

    warningsAsErrors true

    enable 'LogConditional', 'NewerVersionAvailable', 'TypographyQuotes'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven'

group = 'com.github.stoyicker'

task sourcesJar(type: Jar) {
  //noinspection GroovyAssignabilityCheck
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

configurations {
  javadocCompile
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.sourceFiles
  //noinspection GroovyAssignabilityCheck
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.javadocCompile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  //noinspection GroovyAssignabilityCheck
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          //noinspection GroovyAssignabilityCheck
          name 'Creative Commons Attribution 4.0 International'
          url 'https://creativecommons.org/licenses/by/4.0/legalcode'
          distribution 'repo'
        }
      }
    }
  }
}

dependencies {
  //noinspection GroovyAssignabilityCheck
  provided providedDependencies.recyclerView
  //noinspection GroovyAssignabilityCheck
  javadocCompile javadocDependencies.supportAnnotations
}
